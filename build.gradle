plugins
{
    id 'java'
    // Code coverage reports plugin
    id 'net.saliman.cobertura' version '2.3.1'
    // Code coverage report submission to Coveralls service
    id 'com.github.kt3k.coveralls' version '2.6.3'
    // Copy dependencies into a single (shadow'd) jar
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

allprojects
{
    group = 'com.rgi.TilingApp'
    version = '0.0.1-SNAPSHOT'

    repositories
    {
        ///mavenLocal()
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'cobertura'
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies
    {
        // All projects test with JUnit
        testCompile 'junit:junit:4.+'

        // Cobertura reports will not run without the following
        testRuntime "org.slf4j:slf4j-log4j12:1.7.21"
        testRuntime "log4j:log4j:1.2.17"
    }

    cobertura
    {
        coverageIgnoreTrivial = true
    }
}

subprojects
{
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

def files = subprojects.collect{new File(it.projectDir, '/build/cobertura/cobertura.ser')}
cobertura
{
    coverageFormats = ['xml', 'html']
    coverageIgnoreTrivial = true
    coverageSourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
    coverageMergeDatafiles = files
}
test.dependsOn(subprojects.test)
